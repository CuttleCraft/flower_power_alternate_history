title: Campaigning
new-page: true
on-arrival: month_actions += 1
max-visits: 36
is-card: true
card-image: img/flags.jpg
frequency: 100
tags: party_affairs

= Campaigning

Whether or not we are in an electoral campaign, the party is always trying to broaden its voter base. Who should we focus our campaign efforts on?

- @youth: The student youth on campuses nationwide, who typically incline towards the New Left.
- @workers: The unionized working class, who typically incline towards the Old Left.
- @consumer: The typically suburban consumer middle class, who incline towards the establishment.
- @rural: Rural workers and small farmers, who largely detest us.
- @unemployed: The unemployed, including war veterans.
- @black: Afro-Americans, who are the focus of the civil rights movement.
- @root: We either don't want to or cannot campaign right now.
- @easy_discard

@youth
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; youth_dove += 6*(1-dissent); youth_dove -= 2*(1-dissent) if dove_electoral < 1; youth_democrat -= 2 if year < 1969

We focus our campaign through the student youth, organizing on campuses and increasing our presence in universities nationwide.

@workers
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; workers_dove += 6*(1-dissent); workers_dove -= 3*(1-dissent) if afl_cio != 1; workers_dove -= 1*(1-dissent) if left_economics != 1; workers_dove -= 1 if dove_electoral < 1;

We campaign in union halls and workplaces, emphasizing our economic plans as well as our general anti-war stance. [? if afl_cio != 1 : Our lack of connections in organized labor has made the campaign harder, however, and our gains are limited. ?][? if afl_cio == 1 : The AFL-CIO has helped in our campaign greatly, organizing workers under our campaign. ?][? if left_economics != 1 : Our party is seen as a single-issue one, so our campaign has difficulty appealing to workers who are more focused with economic concerns than ending the war. ?]

@consumer
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; consumer_dove += 4*(1-dissent); consumer_dove -= 2 if dove_electoral < 1; consumer_dove -= 1 if antiwar_wave != 1; consumer_dove += 1 if mccarthy_confidence >= 60; principled_party += 1;

We have increased our campaigning among the suburban middle class, attempting to cement our image as a principled progressive and anti-war party, and not radical communists and anarchists who want to destroy the nation. 

@rural
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; rural_dove += 3*(1-dissent); rural_dove -= 2 if dove_electoral < 1; rural_dove -= 1 if antiwar_wave != 1; rural_dove += 1 if mccarthy_confidence >= 70; principled_party += 1;

We have increased our campaigning among farmers by attempting to appear as a principled progressive party, despite their intense hostility towards our "un-American" ideals. Perhaps with time, they can be won over.

@unemployed
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; unemployed_dove += 6*(1-dissent); unemployed_dove -= 2 if left_economics != 1 and not antiwar_wave; unemployed_dove += 3*(1-dissent) if antiwar_wave = 1; unemployed_dove -= 1 if veteran_support != 1;

We have increased our campaigning among the unemployed, including Vietnam War veterans. [? if antiwar_wave : With the hugely mounting opposition to the war, veterans flock to our party. ?]

@black
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: peoples_party == 1
on-arrival: black_dove += 6*(1-dissent); black_dove -= 2 if panther_support != 1; resources -= 1

We have increased our campaigning among Afro-Americans, promising to them desegregation and civil rights.
